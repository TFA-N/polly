root        -> COMMAND (expr (WS expr)*)?            // AST root with a WS separated list of expressions

expr        -> relation '->' ID                      // assignment of relation to identifier X
relation    -> conjunction (REL_OP conjunction)*     // relation (<,>,<=,>=,==)
conjunction -> disjunction (CONJ_OP disjunction)*    // conjunction (||)
disjunction -> secTerm (DISJ_OP secTerm)*            // disjunction (&&)
secTerm     -> term (SECTERM_OP term)*               // plus minus
term        -> factor (TERM_OP factor)*              // multiplication and co
factor      -> postfix (FACTOR_OP factor)?           // right-associative (power operator)
postfix     -> autolist (POSTFIX_OP autolist)+       // postfix operator
autolist    -> dotdot (';' dotdot)*                  // implicit list literal
dotdot      -> unary ('..' unary ('$' unary)?)?      // range operator with optional step size
unary       -> UNARY_OP unary                        // right-associative unary operator
             | access
access      -> call ('.' varOrCall)?                 // namespace access. call must be a single identifier (represented by a VarAccess)
call        -> literal ( '(' parameters ')' )?
literal     -> ID                                    // VarAccess
             | '(' expr ')'                          // braced expression
             | '\(' parameters ')'                   // lambda function literal
             | '{' exprList '}'                      // concrete list of expressions
             | TRUE | FALSE                          // boolean literal
             | CHANNEL                               // channel literal
             | USER                                  // user literal
             | STRING                                // string literal
             | NUMBER                                // number literal
             | DATETIME                              // date literal
             | TIMESPAN                              // timespan literal
             | COMMAND                               // command literal

varOrCall   -> ID                                    // var access
             | ID '(' exprList ')'                   // call   
         
exprList    -> (expr (',' expr)*)?
parameters  -> parameter (',' parameter)*
parameter   -> ID ID                                 // typename, parameter name
             | ID<ID> ID                             // type name, subtype name, parameter name
             | '\(' ID (',' ID)+ ')' ID              // return type name, parameter type names, parameter name
            
            
            
5;8;9[2]